
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.Остатки.Записывать = Истина;
	Движения.СтоимостьТоваров.Записывать = Истина; 
	
	// Создать менеджер временных таблиц
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос; 
	
	// Укажем, какой менеджер временных таблиц использует этот запрос
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТовары.Товар КАК Товар,
	|	РеализацияТовары.Товар.ВидТоваров КАК ВидТовара,
	|	СУММА(РеализацияТовары.Количество) КАК КоличествоВДокументе,
	|	СУММА(РеализацияТовары.Сумма) КАК СуммаВДокументе,
	|	МАКСИМУМ(РеализацияТовары.Цена) КАК Цена
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	Документ.Реализация.Товары КАК РеализацияТовары
	|ГДЕ
	|	РеализацияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТовары.Товар,
	|	РеализацияТовары.Товар.ВидТоваров";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();    
	
	Запрос2 = Новый Запрос;
	
	Запрос2.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос2.Текст = "ВЫБРАТЬ
	                |	ТоварыДокумента.Товар КАК Товар,
	                |	ТоварыДокумента.ВидТовара КАК ВидТовара,
	                |	ТоварыДокумента.КоличествоВДокументе КАК КоличествоВДокументе,
	                |	ТоварыДокумента.СуммаВДокументе КАК СуммаВДокументе,
					|	ТоварыДокумента.Цена КАК Цена,
	                |	ЕСТЬNULL(СтоимостьТоваровОстатки.СтоимостьОстаток, 0) КАК Стоимость,
	                |	ЕСТЬNULL(ОстаткиОстатки.КоличествоОстаток, 0) КАК Количество
	                |ИЗ
	                |	ТоварыДокумента КАК ТоварыДокумента
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьТоваров.Остатки(
	                |				,
	                |				Товар В
	                |					(ВЫБРАТЬ
	                |						ТоварыДокумента.Товар
	                |					ИЗ
	                |						ТоварыДокумента)) КАК СтоимостьТоваровОстатки
	                |		ПО ТоварыДокумента.Товар = СтоимостьТоваровОстатки.Товар
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Остатки.Остатки(
	                |				,
	                |				Товар В
	                |					(ВЫБРАТЬ
	                |						ТоварыДокумента.Товар
	                |					ИЗ
	                |						ТоварыДокумента)) КАК ОстаткиОстатки
	                |		ПО ТоварыДокумента.Товар = ОстаткиОстатки.Товар";
	// Запишем пустые наборы записей, чтобы читать остатки без учета данных в документе
	
	Движения.СтоимостьТоваров.Записать();
	Движения.Остатки.Записать();
	
	РезультатЗапроса = Запрос2.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Количество = 0 Тогда
			СтоимостьТовара = 0;
		Иначе
			СтоимостьТовара = ВыборкаДетальныеЗаписи.Стоимость / ВыборкаДетальныеЗаписи.Количество;
		КонецЕсли;
		
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
		
		Если НЕ ВыборкаДетальныеЗаписи.ВидТовара = Перечисления.ВидыТоваров.Услуги Тогда
			Движение = Движения.Остатки.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Товар = ВыборкаДетальныеЗаписи.Товар;
			Движение.Контрагент = Покупатель;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
			Движение.Цена = ВыборкаДетальныеЗаписи.Цена;
			Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаВДокументе;
			Движение.ДокументОснования = ДокументОбОтгрузке;  
			
			Движение = Движения.СтоимостьТоваров.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Товар = ВыборкаДетальныеЗаписи.Товар;
			Движение.Стоимость = ВыборкаДетальныеЗаписи.КоличествоВДокументе * СтоимостьТовара;
			
		КонецЕсли; 
	КонецЦикла;
	
	
	СформироватьДвиженияПоПартиям(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры   


Процедура 	СформироватьДвиженияПоПартиям(Отказ) 
	Движения.ОстаткиТоваровПоПартиям.Записывать = Истина;
	Движения.ПродажиПоФИФО.Записывать = Истина;
	// 1. Запросом получаем товары из нашего документа "Реализация"
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТовары.Товар,
	|	РеализацияТовары.Товар.ВидТоваров КАК ВидТовара,
	|	СУММА(РеализацияТовары.Сумма) КАК СуммаПродажи,
	|	СУММА(РеализацияТовары.Количество) КАК Количество
	|ИЗ
	|	Документ.Реализация.Товары КАК РеализацияТовары
	|ГДЕ
	|	РеализацияТовары.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТовары.Товар.ВидТоваров,
	|	РеализацияТовары.Товар";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаТоварыДокумента = Запрос.Выполнить().Выбрать();
	
	// 2. Для каждого товара из документа запускаем алгоритм ФИФО
	Пока ВыборкаТоварыДокумента.Следующий() Цикл  
		
		Если ВыборкаТоварыДокумента.ВидТовара = Перечисления.ВидыТоваров.Услуги Тогда
			Продолжить; // Переход к следующей итерации цикла
		КонецЕсли;
		
		ТоварДляСписания = ВыборкаТоварыДокумента.Товар;
		КоличествоКСписанию = ВыборкаТоварыДокумента.Количество;
		РассчитаннаяСебестоимость = 0;
		
		// 3. Получаем остатки партий для этого товара, отсортированные по дате
		ЗапросПартий = Новый Запрос;
		ЗапросПартий.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровПоПартиям.Партия КАК Партия,
		|	ОстаткиТоваровПоПартиям.Партия.МоментВремени КАК МоментВремениПартии,
		|	ОстаткиТоваровПоПартиям.ОстатокТоваровВПартииОстаток КАК КоличествоОстаток,
		|	ОстаткиТоваровПоПартиям.ОбщаяСтоимостьОстаткаПартииОстаток КАК СтоимостьОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровПоПартиям.Остатки(&МоментВремени, Товар = &Товар) КАК ОстаткиТоваровПоПартиям
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремениПартии";
		
		ЗапросПартий.УстановитьПараметр("МоментВремени", МоментВремени());
		ЗапросПартий.УстановитьПараметр("Товар", ТоварДляСписания);
		ВыборкаПартий = ЗапросПартий.Выполнить().Выбрать();
		
		// 4. Запускаем цикл списания по партиям
		Пока ВыборкаПартий.Следующий() И КоличествоКСписанию > 0 Цикл
			
			СебестоимостьЕдиницы = ВыборкаПартий.СтоимостьОстаток / ВыборкаПартий.КоличествоОстаток;
			СписываемоеКоличество = Мин(КоличествоКСписанию, ВыборкаПартий.КоличествоОстаток);
			
			РассчитаннаяСебестоимость = РассчитаннаяСебестоимость + СписываемоеКоличество * СебестоимостьЕдиницы;
			КоличествоКСписанию = КоличествоКСписанию - СписываемоеКоличество;
			
			// 5. Делаем движение "Расход" по регистру партий
			Движение = Движения.ОстаткиТоваровПоПартиям.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Товар = ТоварДляСписания;
			Движение.Партия = ВыборкаПартий.Партия;
			Движение.ОстатокТоваровВПартии = СписываемоеКоличество;
			Движение.ОбщаяСтоимостьОстаткаПартии = СписываемоеКоличество * СебестоимостьЕдиницы;
			
		КонецЦикла;
		
		// 6. Проверка на нехватку
		Если КоличествоКСписанию > 0 Тогда
			Сообщить("Не хватает " + КоличествоКСписанию + " шт. товара '" + ТоварДляСписания + "'");
			Отказ = Истина;
			Продолжить; // Переходим к следующему товару
		КонецЕсли;
		
		// 7. Провлжим движения по регистру Продажи
		ДвижениеПродажи = Движения.ПродажиПоФИФО.Добавить(); 
		ДвижениеПродажи.Период = Дата;
		ДвижениеПродажи.Товар = ВыборкаТоварыДокумента.Товар;
		ДвижениеПродажи.Контрагент = Покупатель;
		ДвижениеПродажи.Количество = ВыборкаТоварыДокумента.Количество;
		ДвижениеПродажи.Выручка = ВыборкаТоварыДокумента.СуммаПродажи;
		ДвижениеПродажи.СебестоимостьПоФИФО = РассчитаннаяСебестоимость;
		
	КонецЦикла;
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Префикс = Обмен.ПолучитьПрефиксНомера();
КонецПроцедуры